// (o2springsecurity/build.gradle)

// Les plugins sont déjà appliqués par la racine.
// On garde 'spring-boot' pour ses fonctionnalités (si besoin).
plugins {
    id 'org.springframework.boot' version '3.5.5' apply false
}

// --- Infos UNIQUES à cette lib ---
version = '1.0.4'
description = 'Loamok Spring Security Library - OAuth2 JWT Authentication'
// --- Fin des infos uniques ---

// --- Dépendances UNIQUES à cette lib ---
dependencies {
    // Dépendances croisées Loamok
    implementation 'org.loamok.libs:emailtemplates:1.0.1'
    // Spring Boot Starters (provided par le projet consommateur)
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-starter-mail'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-rest'
    compileOnly 'org.springframework.boot:spring-boot-starter-security'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.springframework.boot:spring-boot-starter-web'
    
    // Configuration processor pour les proprietes
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    
    // JWT
    api 'io.jsonwebtoken:jjwt-api:0.12.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.7'
    api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    
    // Jackson pour Hibernate
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'
    
    // Tests (Lombok est déjà fourni par la racine)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'

    // swagger v3 (openApi)
    compileOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.12'
}
// --- Fin des dépendances uniques ---

// --- Publication UNIQUE à cette lib ---
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // ===============================================
            // AJOUT DE SÉCURITÉ :
            // Forcer les coordonnées, au lieu de juste
            // faire confiance à "from components.java"
            
            // 'project.group' va hériter 'org.loamok.libs' du parent
            group = rootProject.group 
            
            // 'project.name' va prendre le nom du dossier : 'o2springsecurity'
            artifactId = project.name 
            // ===============================================

            // Le seul bout du POM qui est unique
            pom {
                name = 'Loamok Oauth2 Spring Security' // Nom "humain"
                description = project.description // Récupère la description du haut
            }
        }
    }
}